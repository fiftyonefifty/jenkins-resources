pipeline {
  agent {
    node {
      label 'linux&&docker' // selecting Jenkins node by labels.
    }
  }
  environment {
    AUTH = credentials('secrets') // using Jenkins credential store.
    APPLICATION_NAME = 'awesome-app'
    VERSION = "0.1.0"
    RT_DOCKER_PROD = 'docker-local-prod' // private artifactory repo.
  }
  options {
    buildDiscarder(logRotator(numToKeepStr: '5')) // only keep 5 Jenkins builds.
    timeout(time: 1, unit: 'HOURS')
    timestamps() // add timestamp to Jenkins logs.
  }
  stages {
    // determining which repo to publish to based on branch.
    stage('Prepare Environment') {
      steps {
        script {
          // Setting the target to a default value.
          if(env.BRANCH_NAME != 'master') {
            env.DOCKER_TARGET = 'local'
          }
          // Setting the target to an actual container repo when building the master branch.
          if(env.BRANCH_NAME == 'master') {
            env.DOCKER_TARGET = "${env.RT_DOCKER_PROD}"
          }
          env.CONTAINER = "${env.DOCKER_TARGET}/${env.APPLICATION_NAME}:${env.VERSION}"
          echo "Set container name to: ${env.CONTAINER}"
        }
      }
    }
    // builds the docker container using the versioned tag.
    stage('Build Container') {
      steps {
        sh "docker build --tag ${env.CONTAINER} -f build/Dockerfile ."
      }
    }
    // only runs when the branch is set to `master`.
    // logs into the private repo (Artifactory) and
    // publishes two instances; a versioned container and the `latest` tag.
    stage('Publish Container') {
      when { branch 'master' }
      steps {
        script {
          docker.withRegistry('https://artifactory.company.com', 'secrets') {
            def container = docker.image("${env.CONTAINER}")
            container.push()
            container.push('latest')
          }
        }
      }
    }
  }
  post {
    always {
      echo 'Cleaning up.'
      cleanWs()
      deleteDir()
    }
    success {
      slackSend channel: '#builds',
        color: 'good',
        message: "*Build Succeeded* [${env.BUILD_NUMBER}] \n <${env.BUILD_URL}|${env.JOB_NAME}>"
    }
    failure {
      slackSend channel: '#builds',
        color: 'danger',
        message: "*Build Failed* [${env.BUILD_NUMBER}] \n <${env.BUILD_URL}|${env.JOB_NAME}>"
    }
  }
}
